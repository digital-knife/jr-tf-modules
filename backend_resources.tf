#S3 bucket for backend state
resource "aws_s3_bucket" "terraform_state" {
  bucket = "jr-tf-state-automation-${local.account_id}"
  lifecycle {
    prevent_destroy = true
  }
  tags = local.common_tags
}

#S3 bucket versioning
resource "aws_s3_bucket_versioning" "versioning" {
  bucket = aws_s3_bucket.terraform_state.id
  versioning_configuration {
    status = "Enabled"
  }
}

# Explicitly block all public access to the bucket
resource "aws_s3_bucket_public_access_block" "state_privacy" {
  bucket = aws_s3_bucket.terraform_state.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

# Enable server-side encryption by default
resource "aws_s3_bucket_server_side_encryption_configuration" "state_encryption" {
  bucket = aws_s3_bucket.terraform_state.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

#DynamoDB table for state locking
resource "aws_dynamodb_table" "terraform_locks" {
  name         = "jr-tf-state-locks"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }
  tags = local.common_tags
}
